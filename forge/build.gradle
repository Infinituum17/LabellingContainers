plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "labellingcontainers-common.mixins.json"
        mixinConfig "external.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // FastConfigAPI
    modApi("infinituum.fastconfigapi:fastconfigapi-forge:${rootProject.minecraft_version}-${rootProject.fastconfigapi_version}")
    forgeRuntimeLibrary 'com.moandjiezana.toml:toml4j:0.7.2'
    modApi("infinituum.void_lib:void_lib-forge:${rootProject.minecraft_version}-${rootProject.void_lib_version}")

    // Iron Chests: Restocked
    modCompileOnly "curse.maven:resourceful-lib-570073:4973823"
    modCompileOnly "curse.maven:ironchests-498794:4638051"

    // Iron Chests
    modCompileOnly "curse.maven:iron-chests-228756:4614852"

    // Colossal Chest
    modCompileOnly "curse.maven:colossal-chests-237875:4620461"
    modCompileOnly "curse.maven:cyclops-core-232758:5080553"

    // Echo Chest mod
    modCompileOnly("curse.maven:puzzles-lib-495476:5112685") {
        exclude group: "fuzs.puzzleslib"
    }

    modCompileOnly "curse.maven:puzzles-lib-495476:5112685"
    modCompileOnly "curse.maven:echo-chest-857972:4613440"

    // Nether Chested mod
    modCompileOnly "curse.maven:new-nether-chest-857971:4614038"

    // Compact Storage mod
    modCompileOnly "curse.maven:compactstorage-223703:4732852"

    // Supplementaries mod
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation("io.github.llamalad7:mixinextras-forge:0.4.1")

    modCompileOnly "curse.maven:selene-499980:5254462"
    modCompileOnly "curse.maven:supplementaries-412082:5225134"

    // Sophisticated Storage mod
    modCompileOnly "curse.maven:sophisticated-core-618298:5242817"
    modCompileOnly "curse.maven:sophisticated-storage-619320:5242858"

    // Farmer's delight
    modCompileOnly "curse.maven:farmers-delight-398521:5051242"

    // Fantasy's Furniture
//    modCompileOnly files("libs/commonality-1.20.1-7.0.0.jar")
//    modCompileOnly files("libs/Registrate-MC1.20-1.3.3.jar")
    modCompileOnly "curse.maven:apexcore-550778:4702159"
    modCompileOnly "curse.maven:fantasys-furniture-579564:4702176"

    // Storage Drawers
    modCompileOnly "curse.maven:storage-drawers-223852:6994481"

    // Fluid Drawers
    modCompileOnly "curse.maven:fluid-drawers-legacy-597669:6996766"

    // Framed Compacting Drawers
    // modCompileOnly "curse.maven:framed-compacting-drawers-376351:4777556"
}

processResources {
    var rep_properties = [
            "version"              : rootProject.mod_version,
            "forge_maj_version"    : ((String) rootProject.forge_version).split("-")[1].split("\\.")[0],
            "mc_version"           : rootProject.minecraft_version,
            "arch_version"         : rootProject.architectury_version,
            "pack_format_version"  : rootProject.pack_format_version,
            "modid"                : rootProject.archives_base_name,
            "fastconfigapi_version": rootProject.fastconfigapi_version
    ]

    inputs.properties rep_properties

    filesMatching(["META-INF/mods.toml", "pack.mcmeta"]) {
        expand rep_properties
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
