plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=labellingcontainers"

            runDir "build/datagen"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // FastConfigAPI
    modApi("infinituum.fastconfigapi:fastconfigapi-fabric:${rootProject.minecraft_version}-${rootProject.fastconfigapi_version}")

    // Iron Chests: Restocked
    modCompileOnly "curse.maven:resourceful-lib-570073:4851866"
    modCompileOnly "curse.maven:ironchests-498794:4638050"

    // Mythic Metals Decorations
    def ccapiVersion = "5.2.0"
    def mixinextrasVersion = "0.2.0"
    def aeaVersion = "1.4.0+1.20.0"
    def cpapiVersion = "1.0.0"
    def bejVersion = "1.2.2"

    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${ccapiVersion}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${ccapiVersion}"
    modCompileOnly "io.github.llamalad7:mixinextras-fabric:${mixinextrasVersion}"
    modCompileOnly "maven.modrinth:additionalentityattributes:${aeaVersion}"
    modCompileOnly "eu.pb4:common-protection-api:${cpapiVersion}"

    implementation "blue.endless:jankson:${bejVersion}"

    modCompileOnly "curse.maven:owo-lib-532610:4749199"
    modCompileOnly "curse.maven:cloth-config-348521:4633442"
    modCompileOnly "curse.maven:mythicmetals-410127:4644233"
    modCompileOnly "curse.maven:alloy-forgery-438718:4644228"
    modCompileOnly "curse.maven:mythicmetals-decorations-552128:4580007"

    // Echo chest mod
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-block:${ccapiVersion}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:${ccapiVersion}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${ccapiVersion}"
    implementation 'com.electronwill.night-config:core:3.6.5'
    implementation 'com.electronwill.night-config:toml:3.6.5'

    // From: https://github.com/Fuzss/modresources/tree/main/maven
    modApi "fuzs.puzzlesapi:puzzlesapi-fabric:8.1.5"
    modCompileOnly "curse.maven:forge-config-api-port-fabric-547434:4583000"

    modCompileOnly "curse.maven:puzzles-lib-495476:5112686"
    modCompileOnly "curse.maven:echo-chest-857972:4613441"

    // Nether Chested mod
    modCompileOnly "curse.maven:new-nether-chest-857971:5056973"

    // More Chests mod
    modCompileOnly "curse.maven:more-chests-737045:4978614"

    // Compact Storage mod
    modCompileOnly "curse.maven:compactstorage-223703:4689301"

    // Supplementaries mod
    modCompileOnly "curse.maven:selene-499980:5146530"
    modCompileOnly "curse.maven:supplementaries-412082:5147147"

    // Farmer's delight refabricated
    modCompileOnly("vectorwing:FarmersDelight-Refabricated:1.20.1-2.0.12") {
        exclude(group: "net.fabricmc")
    }

    // Reinforced Chests
    modCompileOnly "curse.maven:reinforced-chests-529873:4594775"
}

processResources {
    var rep_properties = [
            "version"              : rootProject.mod_version,
            "mc_version"           : rootProject.minecraft_version,
            "arch_version"         : rootProject.architectury_version,
            "modid"                : rootProject.archives_base_name,
            "fastconfigapi_version": rootProject.fastconfigapi_version
    ]

    inputs.properties rep_properties

    filesMatching("fabric.mod.json") {
        expand rep_properties
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
